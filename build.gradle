buildscript {
    repositories {
        mavenLocal()
        gradlePluginPortal()
        maven { url "https://repo.spring.io/plugins-release" }
        maven { url "https://repo.spring.io/release" }
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://jcenter.bintray.com/' }
        mavenCentral()
    }
}

plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id "io.freefair.lombok" version "6.3.0"
}

ext {
    configuration = [
            JAVA_VERSION = JavaVersion.VERSION_1_8
    ]
    moduleProjects = subprojects.findAll {it.name.startsWith("mybatisx-")}
    javaProjects = subprojects - project(":parent-bom")
    withoutJclOverSlf4j = {
        exclude group: "org.slf4j", name: "jcl-over-slf4j"
    }
}

configure(allprojects) {project ->
    group = "io.github.mybatisx"
    version = '1.0.0-SNAPSHOT'
    apply plugin: "io.spring.dependency-management"
    apply from: "${rootDir}/.gradle/publish.gradle"
    dependencyManagement {
        imports {
            mavenBom "org.springframework:spring-framework-bom:5.3.9"
            mavenBom "org.junit:junit-bom:5.8.2"
        }
        
        dependencies {
            dependency "com.google.guava:guava:31.1-jre"
            dependency "org.mybatis:mybatis:3.5.4"
            dependency "org.mybatis:mybatis-spring:2.0.5"
            dependency "com.alibaba:fastjson:1.2.72"
            dependency "com.github.jsqlparser:jsqlparser:4.2"
            dependency "com.github.ben-manes.caffeine:caffeine:3.0.5"
            dependency "org.slf4j:slf4j-api:1.7.35"
            dependency "org.slf4j:slf4j-simple:1.7.35"
            dependency "com.google.code.gson:gson:2.9.0"
            dependencySet(group: 'org.mockito', version: '4.3.1') {
                entry('mockito-core') {
                    exclude group: "org.hamcrest", name: "hamcrest-core"
                }
                entry 'mockito-junit-jupiter'
            }
            dependency "io.mockk:mockk:1.12.1"
            dependency "org.assertj:assertj-core:3.22.0"
            dependencySet(group: 'org.apache.logging.log4j', version: '2.17.2') {
                entry 'log4j-api'
                entry 'log4j-core'
                entry 'log4j-jul'
                entry 'log4j-slf4j-impl'
            }
        }
        
        generatedPomCustomization {
            enabled = false
        }

        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
        }
        
        repositories {
            mavenLocal()
            gradlePluginPortal()
            maven { url "https://repo.spring.io/plugins-release" }
            maven { url "https://repo.spring.io/release" }
            maven { url 'https://plugins.gradle.org/m2/' }
            maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
            maven { url 'https://maven.aliyun.com/repository/central' }
            maven { url 'https://jcenter.bintray.com/' }
            mavenCentral()
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
            cacheDynamicVersionsFor 0, "seconds"
        }
    }
}

configure([rootProject] + javaProjects) { project ->
    apply plugin: "java-library"
    apply plugin: "idea"
    //apply plugin: "io.github.mybatisx.build.compile"
    ext.javadocLinks = [
            "https://docs.oracle.com/en/java/javase/17/docs/api/"
    ] as String[]
}

configure(moduleProjects) { project ->
    apply plugin: "io.freefair.lombok"
    apply from: "${rootDir}/gradle/mybatisx-module.gradle"
    
    dependencies {
        implementation fileTree(dir: "libs", include: ["*.jar"])
        testImplementation("org.junit.jupiter:junit-jupiter-api")
        testImplementation("org.junit.jupiter:junit-jupiter-params")
        testImplementation("org.junit.platform:junit-platform-suite-api")
        testImplementation("org.mockito:mockito-core")
        testImplementation("org.mockito:mockito-junit-jupiter")
        testImplementation("io.mockk:mockk")
        testImplementation("org.assertj:assertj-core")
        optional("org.slf4j:slf4j-api")
        testImplementation("org.slf4j:slf4j-api")
        testImplementation("com.alibaba:fastjson")
        // Pull in the latest JUnit 5 Launcher API to ensure proper support in IDEs.
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
        testRuntimeOnly("org.junit.platform:junit-platform-suite-engine")
        testRuntimeOnly("org.apache.logging.log4j:log4j-core")
        testRuntimeOnly("org.apache.logging.log4j:log4j-jul")
        testRuntimeOnly("org.apache.logging.log4j:log4j-slf4j-impl")
    }
}

configure(rootProject) {
    description = "MyBatis Extension Framework"
}
